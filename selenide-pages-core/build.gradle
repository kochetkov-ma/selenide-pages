ext.name = 'Selenide Page Object Framework'
ext.description = 'Framework based on Selenide provided convenient functional to create and manage your app Page Objects in automation testing'

apply from: "$rootDir/gradle/publishing.gradle"

dependencies {
    //// API ////
    api libs.geantyref
    api(libs.selenide) {
        ['selenium-devtools-v85',
         'selenium-devtools-v93',
         'selenium-devtools-v94',
         'selenium-devtools-v95',
         'selenium-firefox-driver',
         'selenium-edge-driver',
         'selenium-firefox-xpi-driver',
         'selenium-ie-driver',
         'selenium-opera-driver',
         'selenium-safari-driver',
         'selenium-edge-driver'
        ].forEach { exclude module: it }
    }
    api(libs.allure.api) {
        exclude group: 'org.slf4j'
    }

    //// IMPL ////
    implementation(libs.allure) { transitive = false }

    testImplementation 'org.testcontainers:selenium'
}

allure {
    adapter {
        allureJavaVersion = libs.versions.allure.get()
        aspectjVersion = "1.9.8.RC3"
        aspectjWeaver = true

        frameworks { junit5 { enabled = false } }
    }
}